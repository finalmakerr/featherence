/* COMPATIBILITY 
   - HLSL compilers
   - Cg   compilers
*/


/*
   Hyllian's xBR level 2 Shader
   
   Copyright (C) 2011/2012 Hyllian/Jararaca - sergiogdb@gmail.com

   This program is free software; you can redistribute it and/or
   modify it under the terms of the GNU General Public License
   as published by the Free Software Foundation; either version 2
   of the License, or (at your option) any later version.

   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU General Public License for more details.

   You should have received a copy of the GNU General Public License
   along with this program; if not, write to the Free Software
   Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.


   Incorporates some of the ideas from SABR shader. Thanks to Joshua Street.
*/

const static float coef           = 2.0;
const static float4 eq_threshold  = float4(15.0, 15.0, 15.0, 15.0);
const static float4 eq_threshold2 = float4(10.0, 10.0, 10.0, 10.0);
const static half y_weight        = 48.0;
const static half u_weight        = 7.0;
const static half v_weight        = 6.0;
const static half3x3 yuv          = half3x3(0.299, 0.587, 0.114, -0.169, -0.331, 0.499, 0.499, -0.418, -0.0813);
const static half3x3 yuv_weighted = half3x3(y_weight*yuv[0], u_weight*yuv[1], v_weight*yuv[2]);

float4 df(float4 A, float4 B)
{
	return float4(abs(A-B));
}

half c_df(half3 c1, half3 c2) {
                        half3 df = abs(c1 - c2);
                        return df.r + df.g + df.b;
                }


bool4 eq(float4 A, float4 B)
{
	return (df(A, B) < eq_threshold);
}

bool4 eq2(float4 A, float4 B)
{
	return (df(A, B) < eq_threshold2);
}


float4 weighted_distance(float4 a, float4 b, float4 c, float4 d, float4 e, float4 f, float4 g, float4 h)
{
	return (df(a,b) + df(a,c) + df(d,e) + df(d,f) + 4.0*df(g,h));
}



struct input
{
	half2 video_size;
	float2 texture_size;
	half2 output_size;
};


struct out_vertex {
	float4 position : POSITION;
	float4 color    : COLOR;
	float2 texCoord : TEXCOORD0;
	float4 t1       : TEXCOORD1;
	float4 t2       : TEXCOORD2;
	float4 t3       : TEXCOORD3;
	float4 t4       : TEXCOORD4;
	float4 t5       : TEXCOORD5;
	float4 t6       : TEXCOORD6;
	float4 t7       : TEXCOORD7;
};

/*    VERTEX_SHADER    */
out_vertex main_vertex
(
	float4 position	: POSITION,
	float4 color	: COLOR,
	float2 texCoord : TEXCOORD0,

   	uniform half4x4 modelViewProj,
	uniform input IN
)
{
	out_vertex OUT;

	OUT.position = mul(modelViewProj, position);
	OUT.color = color;

	float2 ps = float2(1.0/IN.texture_size.x, 1.0/IN.texture_size.y);
	float dx = ps.x;
	float dy = ps.y;

	//    A1 B1 C1
	// A0  A  B  C C4
	// D0  D  E  F F4
	// G0  G  H  I I4
	//    G5 H5 I5


	OUT.texCoord = texCoord;
	OUT.t1 = texCoord.xxxy + half4( -dx, 0, dx,-2.0*dy); // A1 B1 C1
	OUT.t2 = texCoord.xxxy + half4( -dx, 0, dx,    -dy); //  A  B  C
	OUT.t3 = texCoord.xxxy + half4( -dx, 0, dx,      0); //  D  E  F
	OUT.t4 = texCoord.xxxy + half4( -dx, 0, dx,     dy); //  G  H  I
	OUT.t5 = texCoord.xxxy + half4( -dx, 0, dx, 2.0*dy); // G5 H5 I5
	OUT.t6 = texCoord.xyyy + half4(-2.0*dx,-dy, 0,  dy); // A0 D0 G0
	OUT.t7 = texCoord.xyyy + half4( 2.0*dx,-dy, 0,  dy); // C4 F4 I4

	return OUT;
}


/*    FRAGMENT SHADER    */
float4 main_fragment(in out_vertex VAR, uniform sampler2D decal : TEXUNIT0, uniform input IN) : COLOR
{
	bool4 edr, edr_left, edr_up, px; // px = pixel, edr = edge detection rule
	bool4 interp_restriction_lv1, interp_restriction_lv2_left, interp_restriction_lv2_up;
	float4 fx, fx_left, fx_up, final_fx; // inequations of straight lines.
	half3 res1, res2, pix1, pix2;

	float2 fp = frac(VAR.texCoord*IN.texture_size);

	half3 A1 = tex2D(decal, VAR.t1.xw).rgb;
	half3 B1 = tex2D(decal, VAR.t1.yw).rgb;
	half3 C1 = tex2D(decal, VAR.t1.zw).rgb;

	half3 A  = tex2D(decal, VAR.t2.xw).rgb;
	half3 B  = tex2D(decal, VAR.t2.yw).rgb;
	half3 C  = tex2D(decal, VAR.t2.zw).rgb;

	half3 D  = tex2D(decal, VAR.t3.xw).rgb;
	half3 E  = tex2D(decal, VAR.t3.yw).rgb;
	half3 F  = tex2D(decal, VAR.t3.zw).rgb;

	half3 G  = tex2D(decal, VAR.t4.xw).rgb;
	half3 H  = tex2D(decal, VAR.t4.yw).rgb;
	half3 I  = tex2D(decal, VAR.t4.zw).rgb;

	half3 G5 = tex2D(decal, VAR.t5.xw).rgb;
	half3 H5 = tex2D(decal, VAR.t5.yw).rgb;
	half3 I5 = tex2D(decal, VAR.t5.zw).rgb;

	half3 A0 = tex2D(decal, VAR.t6.xy).rgb;
	half3 D0 = tex2D(decal, VAR.t6.xz).rgb;
	half3 G0 = tex2D(decal, VAR.t6.xw).rgb;

	half3 C4 = tex2D(decal, VAR.t7.xy).rgb;
	half3 F4 = tex2D(decal, VAR.t7.xz).rgb;
	half3 I4 = tex2D(decal, VAR.t7.xw).rgb;

	float4 b = mul( half4x3(B, D, H, F), yuv_weighted[0] );
	float4 c = mul( half4x3(C, A, G, I), yuv_weighted[0] );
	float4 e = mul( half4x3(E, E, E, E), yuv_weighted[0] );
	float4 d = b.yzwx;
	float4 f = b.wxyz;
	float4 g = c.zwxy;
	float4 h = b.zwxy;
	float4 i = c.wxyz;

	float4 i4 = mul( half4x3(I4, C1, A0, G5), yuv_weighted[0] );
	float4 i5 = mul( half4x3(I5, C4, A1, G0), yuv_weighted[0] );
	float4 h5 = mul( half4x3(H5, F4, B1, D0), yuv_weighted[0] );
	float4 f4 = h5.yzwx;
	float4 c1 = i4.yzwx;
	float4 g0 = i5.wxyz;

	float4 Ao = float4( 1.0, -1.0, -1.0, 1.0 );
	float4 Bo = float4( 1.0,  1.0, -1.0,-1.0 );
	float4 Co = float4( 1.5,  0.5, -0.5, 0.5 );
	float4 Ax = float4( 1.0, -1.0, -1.0, 1.0 );
	float4 Bx = float4( 0.5,  2.0, -0.5,-2.0 );
	float4 Cx = float4( 1.0,  1.0, -0.5, 0.0 );
	float4 Ay = float4( 1.0, -1.0, -1.0, 1.0 );
	float4 By = float4( 2.0,  0.5, -2.0,-0.5 );
	float4 Cy = float4( 2.0,  0.0, -1.0, 0.5 );


	float3 fp1 = float3(fp.yx, -1);

	float3x4 tt = float3x4(mul(fp1, float3x4(Ao, Bo, Co)), mul(fp1, float3x4(Ao, Bx, Cx)), mul(fp1, float3x4(Ao, By, Cy)));


	interp_restriction_lv1      = ((e!=f) && (e!=h)  && ( !eq2(f,i) || !eq2(f,c) || !eq2(h,i) || !eq2(h,g) || eq(b,c1) && eq(d,g0)) && ( !eq(f,b) && !eq(h,d) || eq(e,i) && !eq(f,i4) && !eq(h,i5) || eq(e,g) || eq(e,c) ) );
	interp_restriction_lv2_left = ((e!=g) && (d!=g));
	interp_restriction_lv2_up   = ((e!=c) && (b!=c));

        float2 delta = float2(IN.video_size.x/IN.output_size.x, 0.5*IN.video_size.x/IN.output_size.x);

	float4 fx45 = saturate((tt[0])/(2*delta.xxxx)+0.5);
	float4 fx30 = saturate((tt[1])/(2*delta.yxyx)+0.5);
	float4 fx60 = saturate((tt[2])/(2*delta.xyxy)+0.5);

//	float4 fx45 = max(0, min(1, (fx      + delta -Co)/(2*delta)));
//	float4 fx30 = max(0, min(1, (fx_left + delta -Cx)/(2*delta)));
//	float4 fx60 = max(0, min(1, (fx_up   + delta -Cy)/(2*delta)));

//	float4 fx45 = smoothstep(Co - delta, Co + delta, fx);
//	float4 fx30 = smoothstep(Cx - delta, Cx + delta, fx_left);
//	float4 fx60 = smoothstep(Cy - delta, Cy + delta, fx_up);


	edr      = (weighted_distance( e, c, g, i, h5, f4, h, f) < weighted_distance( h, d, i5, f, i4, b, e, i)) && interp_restriction_lv1;
	edr_left = ((coef*df(f,g)) <= df(h,c)) && interp_restriction_lv2_left && edr;
	edr_up   = (df(f,g) >= (coef*df(h,c))) && interp_restriction_lv2_up && edr;


	fx45 = edr*fx45;
	fx30 = edr_left*fx30;
	fx60 = edr_up*fx60;

	px = (df(e,f) <= df(e,h));

	float4 maximo = max(max(fx30, fx60), fx45);

        half4x3 pix = half4x3(lerp(E, lerp(H, F, px.x), maximo.x), lerp(E, lerp(F, B, px.y), maximo.y), lerp(E, lerp(B, D, px.z), maximo.z), lerp(E, lerp(D, H, px.w), maximo.w));
	float4 pixel = mul( pix, yuv_weighted[0] );
	

	float4 diff = df(pixel,e);

	half3 res = pix[0];
	float mx = diff.x;

        if (diff.y > mx) {res = pix[1]; mx = diff.y;}
        if (diff.z > mx) {res = pix[2]; mx = diff.z;}
        if (diff.w > mx) {res = pix[3];}

	return float4(res, 1.0);
}


